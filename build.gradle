import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
    id "org.kordamp.gradle.base" version '0.33.0'
}

config {
    info {
        description = 'Spring boot backend'
        scm {
            url = 'https://github.com/smac89/robotsjavabackend'
        }
        organization {
            name = 'smac89'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot', to: project(':backend')
    apply plugin: 'groovy'

    group 'com.github.smac89'
    version '1.0'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.codehaus.groovy') {
                    details.useVersion('3.0.3')
                }
            }
        }
    }

    ext.libraries = [
            groovy          : 'org.codehaus.groovy:groovy',
            spring_boot     : 'org.springframework.boot:spring-boot-starter-jooq',
            spring_hateoas  : 'org.springframework.boot:spring-boot-starter-hateoas',
            spring_test     : 'org.springframework.boot:spring-boot-starter-test',
            postgres        : 'org.postgresql:postgresql',
            flyway          : 'org.flywaydb:flyway-core',
            mapstruct       : 'org.mapstruct:mapstruct:1.3.1.Final',
            mapstruct_proc  : 'org.mapstruct:mapstruct-processor:1.3.1.Final',
            spock           : 'org.spockframework:spock-spring:2.0-M2-groovy-3.0',
            easy_random     : 'org.jeasy:easy-random-core:4.2.0'
    ]
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.3'
}

if (System.getenv('HEROKU_ENV')) {
    apply from: "gradle/config/heroku.gradle", to: project(':backend')
}
