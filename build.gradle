import nu.studer.gradle.jooq.JooqEdition

plugins {
    id 'application'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "com.github.otkmnb2783.dotenv" version "0.0.1"
    id "io.freefair.lombok" version "5.0.0-rc6"
    id 'nu.studer.jooq' version '4.1'
    id "org.flywaydb.flyway" version "6.3.3"
}

group 'com.github.smac89'
version '1.0'

sourceCompatibility = '11'

application {
    mainClassName = 'com.github.smac89.javabackend.Main'
}

jar {
    enabled = true
}

bootJar {
    archiveClassifier.set('boot')
    manifest {
        attributes('Start-Class': mainClassName)
    }
}

dotenv {
    dir = project.rootDir
}

afterEvaluate {
    bootRun {
        //noinspection GroovyAssignabilityCheck
        environment(project.env)

        environment "SPRING_CONFIG_LOCATION", "file:$rootDir/config/"
        environment "SPRING_CONFIG_NAME", "spring"
    }

    flyway {
        /*https://flywaydb.org/documentation/gradle/#environment-variables*/
        user = project.env.POSTGRES_USER
        password = project.env.POSTGRES_PASSWORD
        url = project.env.POSTGRES_DB_URL
        defaultSchema = 'flyway'
        schemas = ['flyway', 'public']
    }

    jooq {
        /*https://github.com/etiennestuder/gradle-jooq-plugin
        https://www.jooq.org/xsd/jooq-codegen-3.13.0.xsd*/
        version = '3.13.0'
        edition = JooqEdition.OSS
        generateSchemaSourceOnCompilation = true
        javaBackend(sourceSets.main) {
            jdbc {
                url = project.env.POSTGRES_DB_URL
                username = project.env.POSTGRES_USER
                password = project.env.POSTGRES_PASSWORD
            }

            logging = 'INFO'

            generator {
                database {
                    inputSchema = 'public'
                }

                generate {
                    indexes = false
                    deprecated = false
                    generatedAnnotation = true
                }

                target {
                    directory = "$buildDir/jooq"
                    packageName = "${project.group}.generated"
                }
            }
        }
    }
}

lombok {
    config.'lombok.addGeneratedAnnotation' = 'true'
    config.'lombok.val.flagUsage' = 'ERROR'
    config.'lombok.var.flagUsage' = 'ERROR'
}

sourceSets {
    main {
        java {
            srcDirs("$buildDir/jooq")
        }
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                /*https://mapstruct.org/documentation/stable/reference/html/*/
                '-Amapstruct.suppressGeneratorTimestamp=true',
                '-Amapstruct.suppressGeneratorVersionInfoComment=false',
                '-Amapstruct.defaultComponentModel=spring',
        ]
    }
}

tasks.withType(nu.studer.gradle.jooq.JooqTask) {
    dependsOn flywayMigrate
}

repositories {
    mavenCentral()
}

ext.env = project.extensions.findByName('env') ?: [
        POSTGRES_USER               : System.getenv('SPRING_DATASOURCE_USERNAME'),
        POSTGRES_PASSWORD           : System.getenv('SPRING_DATASOURCE_PASSWORD'),
        POSTGRES_DB_URL             : System.getenv('SPRING_DATASOURCE_URL'),
]

ext.libraries = [
        spring_boot     : ['org.springframework.boot:spring-boot-starter-jooq',
                           'org.springframework.boot:spring-boot-starter-hateoas'],
        spring_boot_test: ['org.springframework.boot:spring-boot-starter-test'],
        postgres        : ['org.postgresql:postgresql'],
        flyway          : ['org.flywaydb:flyway-core'],
        mapstruct       : ['org.mapstruct:mapstruct:1.3.1.Final'],
        mapstruct_proc  : ['org.mapstruct:mapstruct-processor:1.3.1.Final'],
]

dependencies {
    implementation libraries.spring_boot

    compileOnly libraries.flyway

    jooqRuntime libraries.postgres
    runtimeOnly libraries.postgres

    compileOnly libraries.mapstruct
    annotationProcessor libraries.mapstruct_proc

    testImplementation(libraries.spring_boot_test) {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.3'
}

task stage(dependsOn: ['build', 'clean'], group: 'build') {
    finalizedBy('installDist')
}

tasks.findByName("build")?.mustRunAfter('clean')

installDist {
    destinationDir file("$buildDir/run")
}
