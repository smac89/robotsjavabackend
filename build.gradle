plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "org.flywaydb.flyway" version "6.3.3"
    id "com.github.otkmnb2783.dotenv" version "0.0.1"
    id 'java'
}

group 'com.github.smac89'
version '1.0'
sourceCompatibility = '11'

configurations {
    flywayMigration
}

dotenv {
    dir = project.rootDir
}

flyway {
    // https://flywaydb.org/documentation/gradle/#environment-variables
    configurations = [project.configurations.flywayMigration.name]
    user = project.env.POSTGRES_USER
    password = project.env.POSTGRES_PASSWORD
    url = project.env.POSTGRES_DB_URL
}

repositories {
    mavenCentral()
}

ext.libraries = [
        spring_boot     : ['org.springframework.boot:spring-boot-starter-data-jpa',
                           'org.springframework.boot:spring-boot-starter-hateoas'],
        spring_boot_test: ['org.springframework.boot:spring-boot-starter-test'],
        postgres        : ['org.postgresql:postgresql'],
        flyway          : ['org.flywaydb:flyway-core']
]

dependencies {
    implementation libraries.spring_boot

    flywayMigration libraries.flyway
    flywayMigration libraries.postgres

    testImplementation(libraries.spring_boot_test) {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.3'
}
